#Использовать "../internal/localization"

#Использовать asserts

#Область ОписаниеПеременных

Перем ПредметОтладки;
Перем СерверОтладки;
Перем ЛокализованныеСтроки;

#КонецОбласти

#Область ПрограммныйИнтерфейс

Процедура УстановитьПредметОтладки(ПараметрПредметОтладки) Экспорт
	
	// BSLLS:NestedFunctionInParameters-off
	Утверждения.ПроверитьРавенство(ТипЗнч(ПараметрПредметОтладки), Тип("Строка"), 
		ЛокализованныеСтроки.УтверждениеПредметОтладкиТип);
	// BSLLS:NestedFunctionInParameters-on
	
	Если ЗначениеЗаполнено(ПараметрПредметОтладки) Тогда
		ПредметОтладки = ПараметрПредметОтладки;
	Иначе
		ПредметОтладки = ПредметОтладкиПоУмолчанию();
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьСерверОтладки(ПараметрСерверОтладки) Экспорт
	
	// BSLLS:NestedFunctionInParameters-off
	Утверждения.ПроверитьРавенство(ТипЗнч(ПараметрСерверОтладки), Тип("Строка"),
		ЛокализованныеСтроки.УтверждениеСерверОтладкиТип);
	// BSLLS:NestedFunctionInParameters-on
	
	Если ЗначениеЗаполнено(ПараметрСерверОтладки) Тогда
		СерверОтладки = ПараметрСерверОтладки;
	Иначе
		СерверОтладки = СерверОтладкиПоУмолчанию();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ДобавитьПараметрыКоманды(Команда) Экспорт
	
	Если ЗначениеЗаполнено(ПредметОтладки) Тогда
		Команда.ДобавитьПараметр(СтрШаблон("--infobase=""%1""", ПредметОтладки));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СерверОтладки) Тогда
		Команда.ДобавитьПараметр(СтрШаблон("--debugger=""%1""", СерверОтладки));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриСозданииОбъекта() Экспорт
	
	ПредметОтладки = ПредметОтладкиПоУмолчанию();
	СерверОтладки = СерверОтладкиПоУмолчанию();
	
	ЛокализованныеСтроки = ЛокализованныеРесурсыПараметрыСоединенияЗамера.ЛокализованныеСтроки();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПредметОтладкиПоУмолчанию()
	Возврат "DefAlias";
КонецФункции

Функция СерверОтладкиПоУмолчанию()
	Возврат "http://localhost:1550";
КонецФункции

#КонецОбласти